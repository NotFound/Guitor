#! winxed

// pizarra: a simple drawing tool using WinxedXlib module


$load "Guitor.pbc";

$include "Guitor.winxhead";

using namespace Guitor;
using namespace Guitor.Events;

//**********************************************************************

function evhandler(object, string methodname)
{
    var method = find_method(object, methodname);
    return function (event) { object.*method(event); };
}

//**********************************************************************

class ColorSelector : ChildWindow
{
    var pizarra;
    var colors;
    const int WIDTH = 50, HEIGHT = 82;
    function ColorSelector(pizarra, int x, int y)
    {
        self.pizarra = pizarra;
        string colors[] = [
           "black",  "white",      "grey",
           "red",    "green",      "blue",
           "yellow", "pink",       "orange",
           "violet", "maroon",     "aquamarine",
           "coral",  "lime green", "light steel blue"
        ];
        self.colors = colors;
        self.ChildWindow(pizarra, x, y, WIDTH + 1, HEIGHT + 1);
        self.OnExpose += evhandler(self, "onExpose");
        self.OnButtonPress += evhandler(self, "onButtonPress");
        self.Map();
    }
    function onExpose(event)
    {
        self.SetForeground("black");
        self.DrawLine(    0,      0, WIDTH, 0);
        self.DrawLine(WIDTH,      0, WIDTH, HEIGHT);
        self.DrawLine(WIDTH, HEIGHT,     0, HEIGHT);
        self.DrawLine(    0, HEIGHT,     0, 0);
        var colors = self.colors;
        for (int i = 0; i < 3; ++i)
            for (int j = 0; j < 5; ++j) {
                self.SetForeground("black");
                self.DrawRectangle( 2 + i * 16, 2 + j * 16, 14, 14);
                string color = colors[i * 3 + j];
                self.SetForeground(color);
                self.FillRectangle( 3 + i * 16, 3 + j * 16, 13, 13);
            }
    }
    function onButtonPress(event)
    {
        int x = event.x();
        int y = event.y();
        x = x / 16;
        y = y / 16;
        if (x >= 0 || x < 3 || y >= 0 || y < 5) {
            var colors = self.colors;
            string color = colors[x * 3 + y];
            self.pizarra.setcolor(color);
        }
        self.Destroy();
    }
}

//**********************************************************************

class Thing
{
    var colorspec;
    function Thing(string colorspec)
    {
        self.colorspec = colorspec;
    }
    function setforeground(drawable)
    {
        drawable.SetForeground(self.colorspec);
    }
}

class Line : Thing
{
    var xcoord;
    var ycoord;
    function Line(string colorspec)
    {
        self.Thing(colorspec);
        int x[];
        int y[];
        self.xcoord = x;
        self.ycoord = y;
    }
    function push(int x, int y)
    {
        push(self.xcoord, x);
        push(self.ycoord, y);
    }
    function draw(drawable)
    {
        var x = self.xcoord;
        var y = self.ycoord;
        int n = elements(x) - 1;
        self.setforeground(drawable);
        if (n == 0)
            drawable.DrawPoint(x[0], y[0]);
        else
            for (int i = 0; i < n; ++i)
                drawable.DrawLine(x[i], y[i], x[i + 1], y[i + 1]);
    }
}

class BaseRectangle : Thing
{
    var x0;
    var y0;
    var x1;
    var y1;
    function BaseRectangle(string colorspec, int x0, int y0, int x1, int y1)
    {
        self.Thing(colorspec);
        if (x0 < x1) {
            self.x0 = x0;
            self.x1 = x1;
        }
        else {
            self.x0 = x1;
            self.x1 = x0;
        }
        if (y0 < y1) {
            self.y0 = y0;
            self.y1 = y1;
        }
        else {
            self.y0 = y1;
            self.y1 = y0;
        }
    }
}

class Rectangle : BaseRectangle
{
    function Rectangle(string colorspec, int x0, int y0, int x1, int y1)
    {
        self.BaseRectangle(colorspec, x0, y0, x1, y1);
    }
    function draw(drawable)
    {
        self.setforeground(drawable);
        int x0 = self.x0;
        int x1 = self.x1;
        int y0 = self.y0;
        int y1 = self.y1;
        drawable.DrawRectangle(x0, y0, x1 - x0, y1 - y0);
    }
}

class FillRectangle : BaseRectangle
{
    function FillRectangle(string colorspec, int x0, int y0, int x1, int y1)
    {
        self.BaseRectangle(colorspec, x0, y0, x1, y1);
    }
    function draw(drawable)
    {
        self.setforeground(drawable);
        int x0 = self.x0;
        int x1 = self.x1;
        int y0 = self.y0;
        int y1 = self.y1;
        drawable.FillRectangle(x0, y0, x1 - x0, y1 - y0);
    }
}

//**********************************************************************

class Pizarra : TopLevelWindow
{
    const int LINE = 0, RECTANGLE = 1, FILLRECT = 2;

    var mode;
    var listline;
    var line;
    var pressed;
    var initx;
    var inity;
    var oldx;
    var oldy;
    var colorspec;
    function Pizarra(controller)
    {
        self.mode = LINE;
        self.listline = [];
        string title = "pizarra";
        self.TopLevelWindow(controller, title, 0, 0, 600, 400);
        controller.created(self);
        self.pressed = false;
        self.initx = 0;
        self.inity = 0;
        self.oldx = 0;
        self.oldy = 0;
        self.colorspec = "black";

        self.OnDestroy += function (event)
        {
            self.controller.destroyed(self);
        };
        self.OnConfigure += function (event) { self.onConfigure(event); };
        self.OnExpose += function (event) { self.onExpose(event); };
        self.OnKeyPress += function (event) { self.onKeyPress(event); };
        self.OnButtonPress += function (event) { self.onButtonPress(event); };
        self.OnButtonRelease += function (event) { self.onButtonRelease(event); };
        self.OnMotion += function (event) { self.onMotion(event); };
        self.OnClientMessage += function (event) { self.onClientMessage(event); };
    }

    function onConfigure(event)
    {
        __DEBUG__ && cry(__FUNCTION__,
                " x: ", event.x(),          " y: ", event.y(),
                " width: ", event.width(), "  height: ", event.height());
    }
    function onExpose(event)
    {
        for (var l in self.listline)
            l.draw(self);
    }
    function onKeyPress(event)
    {
        int code = event.keycode();
        var display = self.display;
        string key = display.KeysymToString(display.KeycodeToKeysym(code));
        switch (key) {
          case "Escape":
            self.Destroy();
            break;
          case "c":
            int x = event.x();
            int y = event.y();
            self.opencolorsel(x, y);
            break;
          case "l":
            self.mode =: LINE;
            break;
          case "r":
            self.mode =: RECTANGLE;
            break;
          case "f":
            self.mode =: FILLRECT;
            break;
          case "BackSpace":
            self.pressed =: false;
            if (self.line != null)
                self.line = null;
            if (elements(self.listline) > 0)
                self.listline.pop();
            //TODO else beep, or something.
            self.ClearArea(0, 0, 0, 0, 1);
            break;
          default:
            int sym = self.display.KeycodeToKeysym(code);
            say('Key name: ', self.display.KeysymToString(sym));
        }
    }
    function onButtonPress(event)
    {
        int button = event.button();
        self.pressed = button;
        int oldx = event.x();
        int oldy = event.y();
        self.initx =: oldx;
        self.inity =: oldy;
        self.oldx =: oldx;
        self.oldy =: oldy;
        switch (self.mode) {
          case RECTANGLE:
          case FILLRECT:
            self.SetFunction(0xa);
            self.SetLineAttributes(0, 1, 0, 0);
            self.SetSubwindowMode(1);
            break;
        }
    }
    function onButtonRelease(event)
    {
        if (! self.pressed)
            return;
        self.pressed = false;
        self.line = null;
        int initx = self.initx;
        int inity = self.inity;
        int oldx = self.oldx;
        int oldy = self.oldy;
        string colorspec = self.colorspec;
        switch (self.mode) {
          case RECTANGLE:
          case FILLRECT:
            self.hintrectangle(initx, inity, oldx, oldy);
            self.SetFunction(0x3);
            self.SetLineAttributes(0, 0, 0, 0);
            self.SetSubwindowMode(0);
            var r = self.mode == RECTANGLE ?
                new Rectangle(colorspec, initx, inity, oldx, oldy) :
                new FillRectangle(colorspec, initx, inity, oldx, oldy);
            self.listline.push(r);
            r.draw(self);
            break;
        }
    }
    function onMotion(event)
    {
        if (self.pressed) {
            int x = event.x();
            int y = event.y();
            int oldx = self.oldx;
            int oldy = self.oldy;
            if (x != oldx || y != oldy) {
                switch (self.mode) {
                  case LINE:
                    var line = self.line;
                    if (line == null) {
                        self.line = line = new Line(self.colorspec);
                        self.listline.push(line);
                        line.push(oldx, oldy);
                    }
                    line.push(x, y);
                    line.draw(self);
                    break;
                  case RECTANGLE:
                  case FILLRECT:
                    self.hintrectangle(self.initx, self.inity, oldx, oldy);
                    self.hintrectangle(self.initx, self.inity, x, y);
                    break;
                }
            }
            self.oldx =: x;
            self.oldy =: y;
        }
    }
    function onClientMessage(event)
    {
        say('Bye');
        self.Destroy();
    }

    function hintrectangle(int x0, int y0, int x1, int y1)
    {
        int x = x0;
        int y = y0;
        int width = x1 - x;
        int height = y1 - y;
        if (width && height) {
            if (x > x1) {
                x = x1;
                width = -width; 
            }
            if (y > y1) {
                y = y1;
                height = -height;
            }
            self.DrawRectangle(x, y, width, height);
        }
    }
    function opencolorsel(int x, int y)
    {
        var colorsel = new ColorSelector(self, x, y);
    }
    function setcolor(string spec)
    {
        self.colorspec = spec;
    }
}

//**********************************************************************

class MyController : Controller
{
    var counter;
    function MyController()
    {
        self.Controller();
        self.counter = 0;
    }
    function created(pizarra)
    {
        self.counter++;
    }
    function destroyed(pizarra)
    {
        var counter = self.counter;
        if (--counter == 0)
            self.Quit();
    }
}

//**********************************************************************

function main(args)
{
    int nargs = elements(args);
    var controller = new MyController();

    int n =  (nargs < 2) ? 1 : nargs - 1;
    for (int i = 0; i < n; ++i) {
        var pizarra = new Pizarra(controller);
        pizarra.SetWMProtocols(['WM_DELETE_WINDOW']);
        pizarra.Map();
    }

    controller.MainLoop();

    say('End');
    controller.Close();
}

// End.
